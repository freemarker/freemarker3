PARSER_PACKAGE="freemarker.core.parser";
TREE_BUILDING_ENABLED=false;
EXTRA_TOKENS=DIRECTIVE_END;

/**
 * The lexer portion defines 3 lexical states:
 * DEFAULT, EXPRESSION, DIRECTIVE_NAME
 * The lexer is in the DEFAULT state when it is 
 * just consuming regular text, but is scanning for
 * FreeMarker constructs. 
 * DIRECTIVE_NAME is a trivial state you are in just
 * after a <# or [# and we expect a directive name.
 * EXPRESSION is the state you are in
 * when the parser wants a FreeMarker expression.
 * So, for example, you enter the EXPRESSION state
 * right after a ${ and leave it after the matching }.
 * Or, you enter the EXPRESSION state right after
 * an "<#if" and then, when you hit the matching ">"
 * that ends the if directive, you go back to DEFAULT lexical state.
 */

INJECT LEXER_CLASS : 
   import static freemarker.template.utility.StringUtil.isFTLIdentifier;
{
    /**
    Keeps track of how deeply nested
    we have the hash literals.
    This is necessary since we need to be
    able to distinguish the } used to close
    a hash literal and the one used to
    close a ${
    */
    private int hashLiteralNesting;
    private int parenthesisNesting;
    private int bracketNesting;
    private boolean inPointySyntax;
    // Not being used now. FIXME later.
    @Property boolean onlyTextOutput;
    String templateName;

    private EnumSet<TokenType> directiveNames = EnumSet.of(
        _ATTEMPT, _RECOVER, IF, ELSE_IF, LIST, FOREACH, SWITCH,
        CASE, ASSIGN, GLOBALASSIGN, LOCALASSIGN, SET, _INCLUDE,
        IMPORT, FUNCTION, _MACRO, VISIT, STOP, RETURN, SETTING, 
        EMBED, VAR, ELSE, BREAK, FLUSH, TRIM, LTRIM, RTRIM,
        DEFAUL, NESTED, RECURSE, FALLBACK, ESCAPE, NOESCAPE, 
        FTL_HEADER, UNKNOWN_DIRECTIVE
    );

    private EnumSet<TokenType> emptyDirectives = EnumSet.of(
        _ATTEMPT, _RECOVER, ELSE, BREAK, FLUSH, TRIM, LTRIM, 
        RTRIM, DEFAUL, FALLBACK, NOESCAPE 
    );

/*
    private Token TOKEN_HOOK(Token token) {
        if (token.getType() == POSSIBLE_DIRECTIVE) {
            BASE_TOKEN_CLASS prevToken = token.previousCachedToken();
            if (prevToken.getType() == NON_TRAILING_WHITESPACE && prevToken.getBeginColumn()==1) {
                ((Whitespace) prevToken).setIgnored(true);
            }
            activeTokenTypes.add(EOL);
        } 
        else if (token.getType()==EOL) {
            token.setType(DIRECTIVE_END);
            activeTokenTypes.remove(EOL);
        }
        return token;
    }*/

    private Token TOKEN_HOOK(Token token) {
        if (directiveNames.contains(token.getType())) {
            inPointySyntax = token.charAt(0) == '<';
            if (emptyDirectives.contains(token.getType())) {
                switchTo(LexicalState.DEFAULT);
            }
            else {
                switchTo (LexicalState.EXPRESSION);
            }
        }
        switch(token.getType()) {
            case GREATER_THAN_EQUALS : 
            case GREATER_THAN : 
                if (parenthesisNesting == 0 && inPointySyntax) {
                    if (token.getType() == GREATER_THAN_EQUALS) {
                        token.truncate(1);
                    }
                    token.setType(DIRECTIVE_END);
                    switchTo(LexicalState.DEFAULT);
                }
                break;
            case OPEN_PAREN : 
                parenthesisNesting++;
                switchTo(LexicalState.EXPRESSION);
                break;
            case CLOSE_PAREN :
                parenthesisNesting--;
                break;
            case OPEN_BRACKET : 
                bracketNesting++;
                break;
            case CLOSE_BRACKET : 
                if (bracketNesting > 0) {
                    --bracketNesting;
                } else {
                    token.setType(DIRECTIVE_END);
                    switchTo(LexicalState.DEFAULT);
                }
                break;
            case OPEN_BRACE : 
                ++hashLiteralNesting;
                break;
            case CLOSE_BRACE : 
                if (hashLiteralNesting > 0) {
                    --hashLiteralNesting;
                } else {
                    switchTo(LexicalState.DEFAULT);
                }
                break;
            case UNIFIED_CALL : 
                switchTo(LexicalState.EXPRESSION);
                break;
        }
        return token;
    }
}

UNPARSED :
    <?COMMENT : ("<#--" | "[#--") (~[])* ("--]" | "-->")>
;

MORE :
    <START_TAG : "<#" | "[#"> : DIRECTIVE_NAME
;    

TOKEN :
    <CLOSE_DIRECTIVE_BLOCK : ("[/#" (~["]"])* "]") | ("</#" (~[">"])* ">")> 
    |
    <UNIFIED_CALL : "<@" | "[@" > 
    |
    <UNIFIED_CALL_END : ("<" | "[") "/@" (~[" ", "\t", "\n", "\r", ">", "]"])* (">" | "]")> 
    |
    <OUTPUT_ESCAPE : "${" > : EXPRESSION
    |
    <?NOPARSE : <NOPARSE_BEGIN> (~[])* <NOPARSE_END> > #LegacyNoParse
    |
    <#NOPARSE_BEGIN :   ("<#noparse" (<BLANK>)* ">") | ("/#noparse" (<BLANK>)* "]")>
    |
    <#NOPARSE_END :  ("</#noparse" (<BLANK>)* ">") | ("[/#noparse" (<BLANK>)* "]")>
;

TOKEN#Text :
    <TRAILING_WHITESPACE : (["\t"," "])* "\n"> #freemarker.core.nodes.Whitespace
    |
    <NON_TRAILING_WHITESPACE : (["\t"," "])+> #freemarker.core.nodes.Whitespace
    |
    <PROBLEMATIC_CHAR : "$" | "<" | "["> #Printable
    |
    <POSSIBLE_DIRECTIVE : "#" (["a"-"z","A"-"Z"])+> #Printable
    |
    <REGULAR_PRINTABLE : (~["$", "<", "[" ,"\t", " ", "\n"])+> #Printable
;

<DIRECTIVE_NAME> TOKEN#DirectiveName :
    <#BLANK : [" ", "\t", "\n", "\r"]>
    |
    <#CLOSE_TAG1 : (<BLANK>)* (">" | "]")>
    |
    <#CLOSE_TAG2 : (<BLANK>)* ("/")? (">" | "]")>
    |
    <_ATTEMPT : "attempt" <CLOSE_TAG1>> 
    |
    <_RECOVER : "recover" <CLOSE_TAG1>>
    |
    <IF : "if"> 
    |
    <ELSE_IF : "elseif">
    |
    <LIST : "list"> 
    |
    <FOREACH : "foreach"> 
    |
    <SWITCH : "switch">
    |
    <CASE : "case"> 
    |
    <ASSIGN : "assign">
    |
    <GLOBALASSIGN : "global" > 
    |
    <LOCALASSIGN : "local"> 
    |
    <SET : "set">
    |
    <_INCLUDE : "include"> 
    |
    <IMPORT : "import"> 
    |
    <FUNCTION : "function"> 
    |
    <_MACRO : "macro">
    |
    <VISIT : "visit"> 
    |
    <STOP : "stop"> 
    |
    <RETURN : "return"> 
    |
    <SETTING : "setting"> 
    |
    <EMBED : "embed"> 
    |
    <VAR : "var">
    |
    <ELSE : "else" <CLOSE_TAG2>> 
    |
    <BREAK : "break" <CLOSE_TAG2>> 
    |
    <FLUSH : "flush" <CLOSE_TAG2>> 
    |
    <TRIM : "t" <CLOSE_TAG2>> 
    |
    <LTRIM : "lt" <CLOSE_TAG2>> 
    |
    <RTRIM : "rt" <CLOSE_TAG2>> 
    |
    <DEFAUL : "default" <CLOSE_TAG1>> 
    |
    <NESTED : "nested"> 
    |
    <RECURSE : "recurse">
    |
    <FALLBACK : "fallback" <CLOSE_TAG2>> 
    |
    <ESCAPE : "escape" > 
    |
    <NOESCAPE : "noescape" <CLOSE_TAG1>> 
    |
    <FTL_HEADER : "ftl">
    |
    <UNKNOWN_DIRECTIVE : (["a"-"z","A"-"Z"])+>
;

<EXPRESSION> TOKEN :
    <EOL : "\n">
;

<EXPRESSION> SKIP :
    <IGNORED_WS : " " | "\t" | "\n" | "\r" >
    |
    <?EXPRESSION_COMMENT1 : "<#--" (~[])* "-->">
    |
    <?EXPRESSION_COMMENT2 : "[#--" (~[])* "--]">
;

<EXPRESSION> TOKEN :
  <STRING_LITERAL :
      ("\"" 
          ((~["\\", "\""]) | ("\\" ~[]))*      
      "\"")
      |
      ("'"
          ((~["\\", "'"]) | ("\\" ~[]))*            
      "'")
  > 
  |
  <RAW_STRING : "r" (("\"" (~["\""])* "\"") | ("'" (~["'"])* "'"))>
  |
  <FALSE : "false">
  |
  <TRUE : "true">
  |
  <NULL : "null">
  |
  <INTEGER : (["0"-"9"])+>
  |
  <DECIMAL : <INTEGER> "." <INTEGER>>
  |
  <DOT : ".">
  |
  <DOT_DOT : "..">
  |
  <BUILT_IN : "?">
  |
  <EXISTS : "??">
  |
  <EQUALS : "=">
  |
  <DOUBLE_EQUALS : "==">
  |
  <NOT_EQUALS : "!=">
  |
  <GREATER_THAN : ">">
  |
  <GREATER_THAN_EQUALS: ">=">
  |
  <EMPTY_DIRECTIVE_END : "/>" | "/]"> : DEFAULT
  |
  <LESS_THAN : "lt" | "<" | "&lt;">
  |
  <LESS_THAN_EQUALS : "lte" | "<=" | "&lt;=">
  |
  <ESCAPED_GT: "gt" | "\\gt" |  "&gt;">
  |
  <ESCAPED_GTE : "gte" | "\\gte" | "&gt;=">
  |
  <PLUS : "+">
  |
  <MINUS : "-">
  |
  <TIMES : "*">
  |
  <DOUBLE_STAR : "**">
  |
  <ELLIPSIS : "...">
  |
  <DIVIDE : "/">
  |
  <PERCENT : "%">
  |
  <AND : "&" | "&&" >
  |
  <OR : "|" | "||">
  |
  <EXCLAM : "!">
  |
  <COMMA : ",">
  |
  <SEMICOLON : ";">
  |
  <COLON : ":">
  |
  <OPEN_BRACKET : "[">
  |
  <CLOSE_BRACKET : "]"> 
  |
  <OPEN_PAREN : "("> 
  |
  <CLOSE_PAREN : ")"> 
  |
  <OPEN_BRACE : "{" > 
  |
  <CLOSE_BRACE : "}" >
  |
  <IN : "in">
  |
  <AS : "as">
  |
  <USING : "using">
;

INCLUDE JAVA_IDENTIFIER_DEF

<EXPRESSION> TOKEN:
   <ID: ("@"|<JAVA_IDENTIFIER_START>)("@"|<JAVA_IDENTIFIER_PART>)*>
;
